plugins {
  id "java"
}

repositories {
  mavenCentral()
  maven {
    url "https://heisluft.de/maven/"
  }
}

sourceCompatibility = targetCompatibility = JavaVersion.VERSION_1_6

static def getOrCreate(SourceSetContainer container, String name) {
  try {
    return container.getByName(name)
  } catch(UnknownDomainObjectException ignored) {
    return container.create(name)
  }
}

getOrCreate(sourceSets, 'common')
getOrCreate(sourceSets, 'bootstrap')

configurations.getByName('bootstrapImplementation').extendsFrom(configurations.getByName('implementation'))

tasks.withType(JavaCompile) {
  options.encoding 'UTF-8'
}

dependencies {
  implementation sourceSets.getByName('common').output
  bootstrapRuntimeOnly sourceSets.getByName('main').output
  implementation rootProject
  implementation "org.ow2.asm:asm-tree:7.3.1"
  implementation "org.apache.logging.log4j:log4j-core:2.17.1"
  implementation "com.googlecode.json-simple:json-simple:1.3.0"
  implementation "net.minecraft:launchwrapper:1.12"
}
